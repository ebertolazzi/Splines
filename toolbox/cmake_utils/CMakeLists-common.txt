############################################################################
#                                                                          #
#  file: CMakeLists-common.txt                                             #
#  Copyright (C) 2021                                                      #
#      Enrico Bertolazzi                                                   #
#      Dipartimento di Ingegneria Industriale                              #
#      Universita` degli Studi di Trento                                   #
#      email: enrico.bertolazzi@unitn.it                                   #
#                                                                          #
############################################################################

option( UTILS_GIT_VERSION   "Get version from GIT command"           ON )
option( UTILS_ENABLE_TESTS  "Enable compilation of testing targets"  ON )
option( UTILS_BUILD_SHARED  "Enable shared option compilation"       ON )
option( UTILS_INSTALL_LOCAL "Enable installation on local directory" ON )
option( UTILS_TWEAK_VERSION_IS_TIMESTAMP "Sets tweak version to timestamp" OFF )

set( UTILS_NAMESPACE "utils" CACHE STRING "Sets the namespace used for exports" )
set( TARGETS_TO_BE_INSTALLED )

#
# DEFAULT SETTINGS
#
set( CMAKE_INSTALL_MESSAGE         NEVER )
set( CMAKE_EXPORT_COMPILE_COMMANDS TRUE  )
set( CMAKE_VERBOSE_MAKEFILE        FALSE )

# Always export compile commands in json file
# (this is quite useful for tools like Sourcetrail)
set( CMAKE_EXPORT_COMPILE_COMMANDS     ON )
set( CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS TRUE )

#
# C++ compiler
#
set( CMAKE_CXX_STANDARD_REQUIRED    ON      )
set( CMAKE_CXX_STANDARD             17      )
set( CMAKE_INSTALL_RPATH            $ORIGIN )
set( CMAKE_BUILD_WITH_INSTALL_RPATH ON      )

if( NOT UNIX )
  set( CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON )
endif()

# Always include cmath defines globally
add_compile_definitions(_USE_MATH_DEFINES)

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
  add_compile_definitions(DEBUG)
endif()

#
# LIBRARY VERSION
#
# If option enabled, get version from latest git tag This command will not work
# in case of add_subdirectory, fetch content or in general when included from
# another project
set(
  UTILS_CURRENT_VERSION "1.0"
  CACHE STRING "Default project version, if not taken from git"
)

if ( UTILS_GIT_VERSION AND NOT WIN32 )
  # FIXME: Does not work on Windows 10
  execute_process(
    COMMAND git describe --tags --abbrev=0
    OUTPUT_VARIABLE UTILS_PROJECT_VERSION
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  )
  string(REGEX REPLACE "\n$" "" UTILS_PROJECT_VERSION "${UTILS_PROJECT_VERSION}")
else()
  set( UTILS_BUILD_TIMESTAMP "0" )
  if( UTILS_TWEAK_VERSION_IS_TIMESTAMP )
    string(TIMESTAMP UTILS_BUILD_TIMESTAMP "%y%m%d%H")
  endif()
  set( UTILS_PROJECT_VERSION "${UTILS_CURRENT_VERSION}.${UTILS_BUILD_TIMESTAMP}" )
endif()

if ( UTILS_INSTALL_LOCAL )
  # install in the home of the whole project
  if ( ${CMAKE_HOME_DIRECTORY} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR} )
    set( INSTALL_PREFIX "lib" )
  else()
    set( INSTALL_PREFIX "lib3rd" )
  endif()
  set( PROJECT_PUBLIC_HEADER_DESTINATION "${INSTALL_PREFIX}/include" )
  add_custom_target( post_build_${PROJECT_NAME} )
  add_custom_command(
    TARGET post_build_${PROJECT_NAME} POST_BUILD
    COMMAND cmake --build . --config ${CMAKE_BUILD_TYPE} --target install
  )
  set( CMAKE_INSTALL_PREFIX ${CMAKE_HOME_DIRECTORY} )
else()
  set( INSTALL_PREFIX                    "lib" )
  set( PROJECT_PUBLIC_HEADER_DESTINATION "${INSTALL_PREFIX}/include/${PROJECT_NAME}" )
endif()

message("${PROJECT_NAME} current version = v${UTILS_PROJECT_VERSION}")
